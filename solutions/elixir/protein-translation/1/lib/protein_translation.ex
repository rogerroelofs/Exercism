defmodule ProteinTranslation do
  @codon_map %{
    "UGU" => "Cysteine",
    "UGC" => "Cysteine",
    "UUA" => "Leucine",
    "UUG" => "Leucine",
    "AUG" => "Methionine",
    "UUU" => "Phenylalanine",
    "UUC" => "Phenylalanine",
    "UCU" => "Serine",
    "UCC" => "Serine",
    "UCA" => "Serine",
    "UCG" => "Serine",
    "UGG" => "Tryptophan",
    "UAU" => "Tyrosine",
    "UAC" => "Tyrosine",
    "UAA" => "STOP",
    "UAG" => "STOP",
    "UGA" => "STOP"
  }
  @doc """
  Given an RNA string, return a list of proteins specified by codons, in order.
  """
  @spec of_rna(String.t()) :: {:ok, list(String.t())} | {:error, String.t()}
  def of_rna(rna) do
    codons = rna
    |> String.codepoints
    |> Enum.chunk_every(3)
    |> Enum.map(&Enum.join/1)

    protein = Enum.reduce_while(codons, [], fn elem, acc ->
      case of_codon(elem) do
        {:ok, "STOP"} -> {:halt, acc}
        {:ok, new_elem} -> {:cont, [new_elem | acc]}
        {:error, _} = err -> {:halt, err}
      end
    end)

    if is_list(protein) do
      {:ok, Enum.reverse(protein)}
    else
      {:error, "invalid RNA"}
    end

  end

  @doc """
  Given a codon, return the corresponding protein

  UGU -> Cysteine
  UGC -> Cysteine
  UUA -> Leucine
  UUG -> Leucine
  AUG -> Methionine
  UUU -> Phenylalanine
  UUC -> Phenylalanine
  UCU -> Serine
  UCC -> Serine
  UCA -> Serine
  UCG -> Serine
  UGG -> Tryptophan
  UAU -> Tyrosine
  UAC -> Tyrosine
  UAA -> STOP
  UAG -> STOP
  UGA -> STOP
  """
  @spec of_codon(String.t()) :: {:ok, String.t()} | {:error, String.t()}
  def of_codon(codon) do
    polypeptide = @codon_map[codon]
    case polypeptide do
      nil -> {:error, "invalid codon"}
      _ -> {:ok, polypeptide}
    end
  end
end
